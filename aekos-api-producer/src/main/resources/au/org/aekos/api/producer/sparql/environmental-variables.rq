PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX aekos: <http://www.aekos.org.au/ontology/1.0.0#>
PREFIX api: <http://www.aekos.org.au/api/1.0#>

# Gets the easy part of the environmental variable dataset (just the 1:1 bits)
SELECT
	?decimalLongitude
	?decimalLatitude
	?geodeticDatum
	?locationID
	?locationName
WHERE {
  GRAPH ?g {
    ?slsg a aekos:STUDYLOCATIONSUBGRAPH .
    # location ID
    ?slsg aekos:aekoslocationidentifier ?locationID .
    ?slsg aekos:studylocationidentifier ?locationName .
    # location
    OPTIONAL {
      ?slsg aekos:studylocation/aekos:location/aekos:sampledpoint [
        aekos:computedlongitude ?decimalLongitude1 ;
        aekos:computedlatitude ?decimalLatitude1
      ] .
    }
    OPTIONAL {
      ?slsg aekos:centroidcoordinates [
        aekos:computedlongitude ?decimalLongitude2 ;
        aekos:computedlatitude ?decimalLatitude2
      ] .
    }
    BIND(COALESCE(?decimalLongitude1, ?decimalLongitude2) AS ?decimalLongitude) .
    BIND(COALESCE(?decimalLatitude1, ?decimalLatitude2) AS ?decimalLatitude) .
    BIND('GDA94' as ?geodeticDatum) .
  }
}